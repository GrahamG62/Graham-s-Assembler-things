         MACRO
         KEYOP &word,              . Operand                           +
               &len=,              . Length (min,max)                  +
               &type=              . Validation type

         gblc  &nxto,&nxte         . Next operand, end of next operand
         gbla  &ovect              . Operand vector

         lclb  &f1(8),&f2(8),&f3(8),&f4(8)

&uwrd    setc  (upper '&word')
         aif   ('&uwrd' eq 'DSECT').dsct000

.*     *--------------------------------------------------------------*
.*     * Operand                                                      *
.*     *--------------------------------------------------------------*
.levl000 anop
         aif   ('&nxto' ne '').levl010 . 1st at this level?
&labc    setc  '#ko&sysndx'            . Yes, initial label
&labe    setc  '#kq&sysndx'            .      initial end label
         ago   .levl020

.levl010 anop
&labc    setc  '&nxto'             . Get next label
&labe    setc  '&nxte'             . Get next end label

.levl020 anop
&labn    setc '#kz&sysndx'         . Set new next label
&nxto    setc '&labn'
&labe    setc '#kx&sysndx'         . Set new next end label
&nxte    setc '&labe'

&vect    seta  &ovect              . Set current vector
&ovect   seta  &ovect+1            . Increment operand vector

.levl900 anop                      . End of level section

.*     *--------------------------------------------------------------*
.*     * Isolate operand                                              *
.*     *--------------------------------------------------------------*
.word000 anop
         aif   (t'&word ne 'O').word020
&minl    seta  0                   . Min length = 0
&maxl    seta  0                   . Max length = 0
&rfac    setc  '0c'                . Length of field = 0
&kwrd    setc  ''                  . keyop
         ago   .word900            . Done this word !

.word020 anop
         aif   ('&word'(1,1) eq '''').word030
         mnote 8,'keyop - keyop &word invalid'
         mexit

.word030 anop
         aif   ('&word'(k'&word,1) eq '''').word040
         mnote 8,'keyop - keyop &word invalid'
         mexit

.word040 anop
&kwrd    setc  '&word'(2,k'&word-2)    . Isolate word
&wlen    seta  k'&word-2               . Get word length
.* &rlen    seta  ((&wlen+3)/4)*4         . Round up length to f'wrd
&rfac    setc  'cl&wlen'               . Set dc info
&f1(1)   setb  1                       . Show word supplied

.word900 anop                      . End of word section

.*     *--------------------------------------------------------------*
.*     * Process min. max lengths                                     *
.*     *--------------------------------------------------------------*
.lenf000 anop
         aif   (t'&word eq 'O').lenf100 . No word, skip
         aif   (t'&len ne 'O').lenf010  . Len= provided, go process

&minl    seta  &wlen               . Set min length
&maxl    seta  &wlen               . Set max length
         ago   .lenf900            . And we're done here!

.*     *--------------------------------------------------------------*
.*     * Len= supplied with word                                      *
.*     *--------------------------------------------------------------*
.lenf010 anop
&minl    seta  &wlen               . Set min length
&maxl    seta  &wlen               . Set max length

         aif   (n'&len gt 2).lenf800
         aif   (t'&len(1) eq 'O').lenf020
         aif   (&len(1) gt &wlen).lenf820

&minl    seta  &len(1)             . Set min length
&f1(2)   setb  1                   . Set Explicit min length

.lenf020 anop
         aif   (t'&len(2) eq 'O').lenf900
         aif   (&len(2) gt &wlen).lenf820

&maxl    seta  &len(2)             . Set max length
&f1(3)   setb  1                   . Set Explicit max length
         ago   .lenf900            . And we're done here!

.*     *--------------------------------------------------------------*
.*     * Len= supplied on its own                                     *
.*     *--------------------------------------------------------------*
.lenf100 anop
         aif   (n'&len gt 2).lenf800

&minl    seta  0                   . Set min length
&maxl    seta  0                   . Set max length
         aif   (t'&len(1) eq 'O').lenf120
&minl    seta  &len(1)             . Set min length
&f1(2)   setb  1                   . Set Explicit min length

.lenf120 anop
         aif   (t'&len(2) eq 'O').lenf900

&maxl    seta  &len(2)             . Set max length
&f1(3)   setb  1                   . Set Explicit max length
         ago   .lenf900            . And we're done here!

.lenf800 anop
         mnote 8,'keyop - length is (min,max)'
         mexit

.lenf810 anop
         mnote 8,'keyop - min length greater than word length'
         mexit

.lenf820 anop
         mnote 8,'keyop - max length greater than word length'
         mexit

.lenf900 anop                      . End of length section

.*     *--------------------------------------------------------------*
.*     * Process type=... operand                                     *
.*     *--------------------------------------------------------------*
.type000 anop
         aif   (t'&type eq 'O').genr000

&tlst(1) setc  'N','A','B','X'

&o       seta  1
.type010 anop
&i       seta  1
.type020 anop
&utyp    setc  (upper '&type(&o)')
         aif   ('&utyp' ne '&tlst(&i)').type030
         mnote *,'Entry &utyp set to &i'
&f2(&i)  setb 1
         ago   .type040

.type030 anop
&i       seta  &i+1
         aif   (&i le n'&tlst).type020

         mnote *,'Invalid type &type(&o), valid types are N,A,B,X'

.type040 anop
&o       seta  &o+1
         aif   (&o le n'&type).type010

.type900 anop

.*     *--------------------------------------------------------------*
.*     * Generate table entry                                         *
.*     *--------------------------------------------------------------*
.genr000 anop

         mnote *,'-------------------------------------------------- *'
         mnote *,' Operand &kwrd                                      '
         mnote *,'-------------------------------------------------- *'
&labc    dc    xl1'20'             * Flag = Operand
         dc    al1(&vect)          * Operand vector
         dc    bl1'&f1(1)&f1(2)&f1(3)&f1(4)&f1(5)&f1(6)&f1(7)&f1(8)'
         dc    al4(&labn)          * Next operand
         dc    al2(&labe)          * Length of operand entry
         dc    al1(&minl)          * Minimum length
         dc    al1(&maxl)          * Maximum length
         dc    &rfac.&word         * keyop
&labe    equ   *-&labc             * Length of this keyop entry
         mexit

.*     *--------------------------------------------------------------*
.*     * Map table entry                                              *
.*     *--------------------------------------------------------------*
.dsct000 anop
keyop    dsect ,
koid     ds    xl1                 * Flag
koop     equ   koid,x'20'          *   This is an operand
koend    equ   koid,x'10'          *   End of operands
kovect   ds    xl1                 * Operand vector
koflg1   ds    xl1                 * Flag#1
kof1wd   equ   koflg1,x'80'        *   Word supplied in table
konxt    ds    al4                 * Next operand
koelen   ds    al2                 * Length of operand entry
komin    ds    al1                 * Minimun length of keyop
kolen    ds    al1                 * Maximum (actual) length of keyop
koword   ds    0c                  * The operand itself
         MEND
